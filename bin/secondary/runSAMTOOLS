#!/usr/bin/env perl

#interface for SAMtools variant calling
use strict;
use warnings;
use File::Spec;
use File::Basename qw/basename dirname/;
use FindBin qw/$RealBin/;
use lib File::Spec->catdir($RealBin,"..","..","lib");
use SeqMule::Parallel;
use SeqMule::Utils;
use Pod::Usage qw/pod2usage/;
use Getopt::Long qw/GetOptions/;

my ($advanced,$n,$help,$tmpdir,
    $somatic_flag,
    $bcftools,$samtools,$vcfutils,$vcf,$vcf_filter,$ref,$bed);
my @bam;
@ARGV or pod2usage ({-exitval=>2});
GetOptions(
    #input and output
    'samtools=s'	=>	\$samtools,
    'bcftools=s'	=>	\$bcftools,
    'ref=s'		=>	\$ref,
    'bam=s'		=>	\@bam,
    'vcfutils=s'	=>	\$vcfutils,
    'vcf=s'		=>	\$vcf,
    'callSomatic'	=>	\$somatic_flag,
    #'vcf-filter=s'	=>	\$vcf_filter,
    'bed=s'		=>	\$bed,
    'advanced=s'	=>	\$advanced,
    'n=i'		=>	\$n,
    'tmpdir=s'		=>	\$tmpdir,
    #other
    'help|h'	        =>	\$help,
) or pod2usage({-exitval => 2});
$help and pod2usage ({-verbose=>1});

&SeqMule::Utils::checkOrCreateTmpdir($tmpdir);
my @steps=&SeqMule::Utils::parsePipeline($advanced);
my %options=%{ $steps[$n-1][2] };
my $nofilter_vcf = File::Spec->catfile($tmpdir,rand($$).".nofilter.vcf");

my $cmd;
my @cmd;

if ($somatic_flag) {
    die "two BAM required for somatic calling\n" unless @bam == 2;
}

$cmd.="$samtools mpileup -SuD -f $ref ";
$cmd.=" $options{SAMadditional} " if $options{SAMadditional} ne '';
$cmd.=" -l $bed " if $bed;
$cmd .= " @bam";
$cmd.=" -C $options{bwaDownGrade}" if ( ($options{bwaDownGrade}) && grep { /_bwa/} @bam); #/_bwa/ means bam is generated by bwa or bwamem
$cmd.=" | $bcftools view ";
$cmd.=" -vcg ";
$cmd .= " -T pair " if $somatic_flag;
$cmd.=" $options{BCFadditional} " if $options{BCFadditional} ne '';
#$cmd.=" - > $vcf";
$cmd.=" - > $nofilter_vcf";
push @cmd,$cmd;$cmd="";

#filtration
$cmd.="$^X $vcfutils varFilter";
$cmd.=" -d $options{minDepth}" if $options{minDepth};
$cmd.=" -1 $options{minStrandBias}" if $options{minStrandBias};
$cmd.=" -2 $options{minBaseQBias}" if $options{minBaseQBias};
$cmd.=" -3 $options{minMapQBias}" if $options{minMapQBias};
$cmd.=" -4 $options{minEndDistBias}" if $options{minEndDistBias};
$cmd.=" $options{FILTERadditional} " if $options{FILTERadditional} ne '';
$cmd.=" $nofilter_vcf | $^X -p -e 's/([^\\t]+\\t[^\\t]+\\t[^\\t]+\\t[^\\t]+\\t[^\\t]+\\t[^\\t]+\\t)\\./\$1PASS/' > $vcf";
push @cmd,$cmd;$cmd="";

push @cmd,"rm -f $nofilter_vcf ";
my $script=&SeqMule::Parallel::genTempScript(@cmd);
exec $script;

__END__
